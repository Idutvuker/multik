// taken from https://github.com/JetBrains-Research/viktor

ext {
    openblasBranch = 'v0.3.12'
    openblasRoot = '0.3.12'
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'de.undercouch:gradle-download-task:3.4.3'
    }
}

apply plugin: de.undercouch.gradle.tasks.download.DownloadTaskPlugin


task downloadOpenBlas(type: de.undercouch.gradle.tasks.download.Download) {
    src "https://github.com/xianyi/OpenBLAS/archive/${openblasBranch}.zip"
    dest new File(buildDir, "openblas.${openblasBranch}.zip")
    overwrite false
}

task unzipOpenBlas(dependsOn: downloadOpenBlas, type: Copy) {
    from zipTree(downloadOpenBlas.dest)
    into buildDir
    outputs.dir new File(buildDir as File, "openblas-$openblasRoot")
}

def nullOutputStream = new OutputStream() {
    @Override
    public void write(int b) {}
}

task installOpenBlas(dependsOn: unzipOpenBlas) {
    def wrkDirOpenBlas = "$buildDir/OpenBLAS-${openblasRoot}"
    outputs.dir new File(buildDir, 'openblas')
    doLast {
        exec {
            workingDir wrkDirOpenBlas
            standardOutput = nullOutputStream
            commandLine("make")
        }
        exec {
            workingDir wrkDirOpenBlas
            standardOutput = nullOutputStream
            commandLine("make", "install", "PREFIX=$buildDir/openblas")
        }
    }
}